varnishtest "Tests multiple servers (shared pool)"

server s1 {
   rxreq
   txresp
} -repeat 2 -start

varnish v1 -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new master1 = redis.db("${redis_master1_address}", 500, 0, 0, 0, true, 1, false, 0);
        new master2 = redis.db("${redis_master2_address}", 500, 0, 0, 0, true, 1, false, 0);
    }

    sub vcl_deliver {
        # SET (master1) -> GET (master1) -> GET (master2).
        master1.command("SET");
        master1.push("foo");
        master1.push("Hello world!");
        master1.execute();

        master1.command("GET");
        master1.push("foo");
        master1.execute();
        set resp.http.Reply-1 = master1.get_reply();

        master2.command("GET");
        master2.push("foo");
        master2.execute();
        set resp.http.Reply-2 = master2.get_reply();

        # SET (master2) -> GET (master2) -> GET (master1).
        master2.command("SET");
        master2.push("bar");
        master2.push("Hello world!");
        master2.execute();

        master2.command("GET");
        master2.push("bar");
        master2.execute();
        set resp.http.Reply-3 = master2.get_reply();

        master1.command("GET");
        master1.push("bar");
        master1.execute();
        set resp.http.Reply-4 = master1.get_reply();
    }
} -start

client c1 {
    txreq
    rxresp

    expect resp.http.Reply-1 == "Hello world!"
    expect resp.http.Reply-2 == ""

    expect resp.http.Reply-3 == "Hello world!"
    expect resp.http.Reply-4 == ""
} -run

varnish v1 -expect client_req == 1
