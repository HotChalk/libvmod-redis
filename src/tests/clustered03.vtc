varnishtest "Tests general execution of commands (shared pool)"

server s1 {
   rxreq
   txresp
} -repeat 1 -start

varnish v1 -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db = redis.db("cluster", "${redis_master1}", 500, 0, 0, 0, 16, true, 32);
        db.add_cserver("${redis_master2}");
        db.add_cserver("${redis_master3}");
    }

    sub vcl_deliver {
        # SET.
        db.command("SET");
        db.push("${redis_key_in_master1}");
        db.push("hello");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-1 = db.get_status_reply();
        }

        # SETEX.
        db.command("SETEX");
        db.push("${redis_key_in_master2}");
        db.push("3600");
        db.push("Hello world!");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-2 = db.get_status_reply();
        }

        # GET.
        db.command("GET");
        db.push("${redis_key_in_master2}");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.Reply-3 = db.get_string_reply();
        }

        # DEL.
        db.command("DEL");
        db.push("${redis_key_in_master2}");
        db.execute();
        if (db.reply_is_integer()) {
            set resp.http.Reply-4 = db.get_integer_reply();
        }

        # MGET.
        db.command("MGET");
        db.push("${redis_key_in_master1}");
        db.push("{${redis_key_in_master1}}:other");
        db.execute();
        if (db.reply_is_array()) {
            set resp.http.Reply-5-Length = db.get_array_reply_length();
            set resp.http.Reply-5-Value-1 = db.get_array_reply_value(0);
            set resp.http.Reply-5-Value-2 = db.get_array_reply_value(1);
        }

        # MGET (CROSSSLOT).
        db.command("MGET");
        db.push("${redis_key_in_master1}");
        db.push("${redis_key_in_master2}");
        db.execute();
        if (db.reply_is_error()) {
            set resp.http.Reply-6 = "1";
        }

        # HMSET.
        db.command("HMSET");
        db.push("${redis_key_in_master2}");
        db.push("field1");
        db.push("Hello world!");
        db.push("field2");
        db.push("42");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-7 = db.get_status_reply();
        }

        # HMGET.
        db.command("HGET");
        db.push("${redis_key_in_master2}");
        db.push("field1");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.Reply-8 = db.get_string_reply();
        }

        # INCR.
        db.command("INCR");
        db.push("${redis_key_in_master1}");
        db.execute();
        if (db.reply_is_error()) {
            set resp.http.Reply-9 = db.get_error_reply();
        }

        # EVAL.
        set req.http.Script = {"
            redis.call('SET', KEYS[1], ARGV[1]);
            redis.call('SET', KEYS[2], ARGV[1]);
        "};
        db.command("EVAL");
        db.push(req.http.Script);
        db.push("2");
        db.push("${redis_key_in_master1}");
        db.push("{${redis_key_in_master1}}:other");
        db.push("Atomic!");
        db.execute();
        if (db.reply_is_nil()) {
            set resp.http.Reply-10 = "o/";
        }

        # EVAL.
        db.command("EVAL");
        db.push(req.http.Script);
        db.push("2");
        db.push("${redis_key_in_master1}");
        db.push("{${redis_key_in_master1}}:other");
        db.push("Atomic x 2!");
        db.execute();
        if (db.reply_is_nil()) {
            set resp.http.Reply-11 = "o/";
        }

        # EVAL (CROSSSLOT).
        set req.http.Script = {"
            redis.call('SET', KEYS[1], ARGV[1]);
            redis.call('SET', KEYS[2], ARGV[1]);
        "};
        db.command("EVAL");
        db.push(req.http.Script);
        db.push("2");
        db.push("${redis_key_in_master1}");
        db.push("${redis_key_in_master2}");
        db.push("Atomic!");
        db.execute();
        if (db.reply_is_error()) {
            set resp.http.Reply-12 = "1";
        }

        # GET.
        db.command("GET");
        db.push("${redis_key_in_master1}");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.Reply-13 = db.get_string_reply();
        }
    }
} -start

client c1 {
    txreq
    rxresp

    expect resp.http.Reply-1 == "OK"

    expect resp.http.Reply-2 == "OK"

    expect resp.http.Reply-3 == "Hello world!"

    expect resp.http.Reply-4 == "1"

    expect resp.http.Reply-5-Length == "2"
    expect resp.http.Reply-5-Value-1 == "hello"
    expect resp.http.Reply-5-Value-2 == ""

    expect resp.http.Reply-6 == "1"

    expect resp.http.Reply-7 == "OK"

    expect resp.http.Reply-8 == "Hello world!"

    expect resp.http.Reply-9 != ""

    expect resp.http.Reply-10 == "o/"

    expect resp.http.Reply-11 == "o/"

    expect resp.http.Reply-12 == "1"

    expect resp.http.Reply-13 == "Atomic x 2!"
} -run

varnish v1 -expect client_req == 1
