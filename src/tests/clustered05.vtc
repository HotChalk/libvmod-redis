varnishtest "Tests handling of ASK reply messages"

server s1 {
   rxreq
   txresp
} -repeat 3 -start

varnish v1 -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db = redis.db("cluster", "${redis_master1}", 500, 0, 0, 0, 16, true, 32);
        db.add_cserver("${redis_master2}");
        db.add_cserver("${redis_master3}");
    }

    sub vcl_deliver {
        # Set key in master1.
        db.command("SET");
        db.push("${redis_key_in_master1}");
        db.push("hello");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-1 = db.get_status_reply();
        }
    }

    sub vcl_fini {
        db.fini();
    }
} -start

client c1 {
    txreq
    rxresp

    expect resp.http.Reply-1 == "OK"
} -run

varnish v2 -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db = redis.db("master1", "${redis_master1}", 500, 0, 0, 0, 16, true, 32);
        db.add_server("master2", "${redis_master2}", 500, 0);
    }

    sub vcl_deliver {
        # Get the key's containing slot.
        db.command("CLUSTER");
        db.push("KEYSLOT");
        db.push("${redis_key_in_master1}");
        db.execute();
        if (db.reply_is_integer()) {
            set resp.http.Slot = db.get_integer_reply();
        }

        # Get the ID for the original node (master1).
        db.command("CLUSTER");
        db.push("NODES");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.NodeOrig = regsub(db.get_reply(), "(?s)^(?:.+\n)?([0-9a-f]+) ${redis_master1} .*$", "\1");
            if (resp.http.NodeOrig !~ "[0-9a-f]+") {
                unset resp.http.NodeOrig;
            }
        }

        # Get the ID for the destination node (master2).
        db.command("CLUSTER");
        db.push("NODES");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.NodeDest = regsub(db.get_reply(), "(?s)^(?:.+\n)?([0-9a-f]+) ${redis_master2} .*$", "\1");
            if (resp.http.NodeDest !~ "[0-9a-f]+") {
                unset resp.http.NodeDest;
            }
        }

        # Mark the destination node as IMPORTING.
        db.command("CLUSTER");
        db.server("master2");
        db.push("SETSLOT");
        db.push(resp.http.Slot);
        db.push("IMPORTING");
        db.push(resp.http.NodeOrig);
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-4 = db.get_status_reply();
        }

        # Mark the original node as MIGRATING.
        db.command("CLUSTER");
        db.server("master1");
        db.push("SETSLOT");
        db.push(resp.http.Slot);
        db.push("MIGRATING");
        db.push(resp.http.NodeDest);
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-5 = db.get_status_reply();
        }

        # Migrate key.
        db.command("MIGRATE");
        db.server("master1");
        db.push(regsub("${redis_master2}", "([\d\.]*):([\d]*)", "\1"));
        db.push(regsub("${redis_master2}", "([\d\.]*):([\d]*)", "\2"));
        db.push("${redis_key_in_master1}");
        db.push("0");
        db.push("0");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-6 = db.get_status_reply();
        }

        # Check that the original node replies with an ASK redirection.
        db.command("GET");
        db.server("master1");
        db.push("${redis_key_in_master1}");
        db.execute();
        if (db.reply_is_error()) {
            if (db.get_error_reply() ~ "^ASK ") {
                set resp.http.Reply-7 = "ASK";
            }
        }
    }

    sub vcl_fini {
        db.fini();
    }
} -start

client c2 -connect ${v2_sock} {
    txreq
    rxresp

    expect resp.http.Slot != <undef>

    expect resp.http.NodeOrig != <undef>

    expect resp.http.NodeDest != <undef>

    expect resp.http.Reply-4 == "OK"

    expect resp.http.Reply-5 == "OK"

    expect resp.http.Reply-6 == "OK"

    expect resp.http.Reply-7 == "ASK"
} -run

varnish v3 -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db = redis.db("cluster", "${redis_master1}", 500, 0, 0, 0, 16, true, 32);
        db.add_cserver("${redis_master2}");
        db.add_cserver("${redis_master3}");
    }

    sub vcl_deliver {
        # Get the key (should internally handle the ASK redirection).
        db.command("GET");
        db.push("${redis_key_in_master1}");
        db.execute();
        if (db.reply_is_string()) {
            set resp.http.Reply-8 = db.get_string_reply();
        }
    }

    sub vcl_fini {
        db.fini();
    }
} -start

client c3 -connect ${v3_sock} {
    txreq
    rxresp

    expect resp.http.Reply-8 == "hello"
} -run

varnish v1 -expect client_req == 1
varnish v2 -expect client_req == 1
varnish v3 -expect client_req == 1
